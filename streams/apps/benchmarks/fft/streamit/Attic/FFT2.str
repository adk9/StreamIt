complex->complex filter CombineDFT(int n)
{  
    complex[n] Ws;

    init
    {
	complex wn = exp(2i * pi / n);	
	Ws[0].real=1;
	Ws[0].imag=0;
	for (int i=1; i < n; i++)
	    Ws[i]=Ws[i-1]*wn;	
    }

    work push n peek n pop n 
    {
        int i;
	complex[n] results;

	for (i = 0; i < n/2; i ++)
        {
	    complex y0;
	    complex y1;
	    complex y1t;
	    
	    y0=peek(i);
	    y1=peek(n/2+i);
	    
	    y1t=y1*Ws[i];

            results[i] = y0 + y1t;
        
            results[n/2 + i] = y0-y1t;
	    
         }

        for (i = 0; i < n; i++)
        {
	    pop();
	    push(results[i]);
        }
    }
}

complex->complex filter FFTReorderSimple(int n)
{
    work peek n push n pop n
    {
        int i;
        
        for (i = 0; i < n; i+=2)
        {
            push(peek(i));
        }
        
        for (i = 1; i < n; i+=2)
        {
            push(peek(i));
        }
        
        for (i=0;i<n;i++)
        {
            pop();
        }
    }
}

complex->complex pipeline FFTReorder(int nWay) 
{
    int n = nWay;
    while (n > 2)
    {
        add (new FFTReorderSimple (n));
	n /= 2;
    }    
}

complex->complex pipeline FFT2 (int nWay)
{
        add (new FFTReorder (nWay));
        int n = 2;
        while (n <= nWay)
        {
            add (new CombineDFT (n));
            n = n * 2;
        }
    
}

/*

public class FFT2 extends StreamIt
{
    public static void main(String[] args)
    {
        new FFT2().run(args);
    }
    public void init()
    {
        add(new OneSource());
        add(new FFTKernel2(16));
        add(new FloatPrinter());
    }
}


*/








