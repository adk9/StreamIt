#!/usr/bin/perl -w
#
# knit: StreamIt compiler wrapper
# David Maze <dmaze@cag.lcs.mit.edu>
# $Id: knit,v 1.10 2002-11-13 20:37:04 dmaze Exp $
#

use strict;
use vars qw(%opts);

use Getopt::Long;
Getopt::Long::Configure("bundling", "no_ignore_case");

get_options() or die "Error parsing options";
if ($opts{help})
  {
    usage(\*STDOUT);
    exit(0);
  }
my $fns = \@ARGV;
$fns = new_to_old($fns) unless $opts{java};
$fns = old_to_c($fns);
c_to_out($fns, $opts{output}) unless $opts{raw};
exit(0);

sub usage
  {
    my ($fh) = @_;
    print $fh <<'EOT';
knit: StreamIt compiler wrapper

Usage: knit [options] file.str ...
	
Options:
  --help              Show this screen
  --java              Input file is "old" syntax
  --output filename   Write a binary to filename
  --raw n             Compile for RAW, with an n-by-n grid
  --verbose           Print commands as they are executed

Options passed to Kopi:
  --constprop, -c     Turns on StreamIt Field Prop
  --fusion, -f        Perform filter fusion
  --linearanalysis    Perform linear analysis
  --linearreplacement Perform linear transformations based on analysis
  --noanneal, -N      Do not run simulated annealing for layout
  --partition, -a     Automatically partition stream graph
  --ratematch, -x     Turn on rate matching for RAW
  --simplesjfusion    Revert to the old, simple SplitJoin fusion algorithm
  --sync, -k          Turn on sync removal
  --unroll, -u        StreamIt Unroll
  --viram, -V         Compile for VIRAM, a vector uniprocessor
EOT
}

sub get_options
  {
    $opts{home} = $ENV{STREAMIT_HOME};
    $opts{output} = "a.out";
    GetOptions(\%opts,
	       "help",
	       "java!",
	       "old-converter",
	       "output|o=s",
	       "raw|r=i",
	       "verbose|v",
	       "constprop|c",
	       "fusion|f",
	       "linearanalysis|i",
	       "linearreplacement|I",
	       "noanneal|N",
	       "partition|a",
	       "ratematch|x",
	       "simplesjfusion|S",
	       "sync|k",
	       "unroll|u",
	       "viram|V");
  }

sub call
  {
    my ($cmd) = @_;
    print "$cmd\n" if $opts{verbose};
    return system($cmd);
  }

sub change_extension
  {
    my ($old, $from, $to) = @_;
    my $fn = $old;
    $fn =~ s/.$from$//;
    $fn .= ".$to";
    return $fn;
  }

sub new_to_old
  {
    my ($fns) = @_;
    my @out = ();
    foreach my $str (@$fns)
      {
	my $java = change_extension($str, "str", "java");
	if ($opts{'old-converter'})
	  {
	    call("java streamit.frontend.ToJava < $str > $java");
	  }
	else
	  {
	    call("java streamit.frontend.ToJava --full $str --output $java");
	  }
	push @out, $java;
      }
    return \@out;
  }

sub old_to_c
  {
    my ($fns) = @_;
    my @out = ();
    my $kjcopts = get_kjc_opts();
    foreach my $java (@$fns)
      {
	if (!$opts{raw})
	  {
	    my $c = change_extension($java, "java", "c");
	    call("java -Xmx512M at.dms.kjc.Main -s $kjcopts $java > $c");
	    push @out, $c;
	  }
	else
	  {
	    call("java -Xmx512M at.dms.kjc.Main -s $kjcopts $java");
	  }
      }
    return \@out;
  }

sub c_to_out
  {
    my ($fns, $target) = @_;
    my $sh = $opts{home};
    call("gcc -g -o$target -I$sh/library/c " . join(' ', @$fns) .
	 " -L$sh/library/c -lstreamit -lm");
  }

sub get_kjc_opts
  {
    my $kjcopts = "";
    $opts{raw} && ($kjcopts .= " -raw " . $opts{raw});
    $opts{constprop} && ($kjcopts .= " -c");
    $opts{fusion} && ($kjcopts .= " -o");
    $opts{linearanalysis} && ($kjcopts .= " -i");
    $opts{linearreplacement} && ($kjcopts .= " -I");
    $opts{noanneal} && ($kjcopts .= " -N");
    $opts{partition} && ($kjcopts .= " -a");
    $opts{ratematch} && ($kjcopts .= " -x");
    $opts{simplesjfusion} && ($kjcopts .= " -S");
    $opts{sync} && ($kjcopts .= " -k");
    $opts{unroll} && ($kjcopts .= " -u");
    $opts{viram} && ($kjcopts .= " -V");
    return $kjcopts;
}
